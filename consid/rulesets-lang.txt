lex-lang:
/<![a-z]>/          ruleset-type
/-- .*$/            comment
/[_a-z]+/           keyword
/'.*'/              string
/[0-9]+/            number
/\./                connection-operator
/\,/                list-operator
/\{/                block-begin
/\}/                block-end
/\n/                newline

/\$[0-9]+/          fighter-number
/\%[0-9]+/          fighter-placement
/\#[a-z0-9A-Z]+/    match-id

identifying keyword by value

parse-lang:
S 			        -> <ruleset-type> ruleset
ruleset             -> rule ruleset
                     | rule
rule                -> name-rule
                     | require-rule
                     | alloc-rule
                     | match-rule
                     | order-rule
                     | score-rule
                     | playoff-rule
player-code         -> <fighter-number>
                     | <fighter-placement>
                     | <match-id> <connection-operator> <keyword:winner>
                     | <match-id> <connection-operator> <keyword:loser>

name-rule           -> <keyword:name> <string>

require-rule        -> <keyword:require> requirement
requirement         -> <keyword:exact> number
                     | <keyword:min> number <keyword:max> number
                     | <keyword:max> number

alloc-rule          -> <keyword:alloc> <fighter-number>

match-rule          -> <keyword:match> <match-id> <block-begin> match-block <block-end>
match-block         -> mb-rule match-block
mb-rule             -> mb-white
                     | mb-blue
                     | mb-type
mb-white            -> <keyword:white> player-code
mb-blue             -> <keyword:blue> player-code
mb-type             -> <keyword:is> list-of-match-types
list-of-match-types -> match-type <list-operator> list-of-match-types
                     | match-type
match-type          -> <keyword:playoff>
                     | <keyword:final>
                     | <keyword:semifinal>
                     | <keyword:repechage>
                     | <keyword:thirdfinal>

order-rule          -> <keyword:order> order-list
order-list          -> order-entry <list-operator> order-list
                     | order-entry
order-entry         -> <match-id>
                     | <keyword:_clip>

score-rule          -> <keyword:score> <block-begin> score-block <block-end>
score-block         -> sb-rule score-block
                     | sb-rule
sb-rule             -> sb-calc
                     | sb-resolve
                     | sb-first
                     | sb-second
                     | sb-third
                     | sb-fifth
sb-calc             -> <keyword:calc> <keyword:points> <keyword:among> calc-group
# for now:
calc-group          -> <keyword:all>
sb-resolve          -> <keyword:resolve> <keyword:playoff>
                     | <keyword:resolve> <match-id>
sb-first            -> <keyword:first> player-code
sb-second           -> <keyword:second> player-code
sb-third            -> <keyword:third> player-code
                     | <keyword:third> player-code <list-operator> player-code
sb-fifth            -> <keyword:fifth> player-code
                     | <keyword:fifth> player-code <list-operator> player-code

playoff-rule        -> <keyword:playoff> <block-begin> playoff-block <block-end>
playoff-block       -> pob-rule playoff-block
                     | pob-rule
pob-rule            -> pob-condition
                     | pob-realloc
                     | match-rule
                     | order-rule
                     | pob-first
                     | pob-second
                     | pob-third
pob-condition       -> <keyword:if> pob-cond-equal2
                     | <keyword:if> pob-cond-equal3
pob-cond-equal2     -> <keyword:equal> <keyword:at> player-code <list-operator> player-code
pob-cond-equal3     -> <keyword:equal> <keyword:at> player-code <list-operator> player-code <list-operator> player-code
pob-realloc         -> <keyword:realloc> pob-realloc-list
pob-realloc-list    -> <fighter-number> <list-operator> pob-realloc-list
                     | <fighter-number>
pob-first           -> <keyword:first> player-code
pob-second          -> <keyword:second> player-code
pob-third           -> <keyword:third> player-code